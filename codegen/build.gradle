plugins {
    id "com.moowork.node" version "1.1.1"
}

node {
    version = "6.10.2"
    download = true
}

task clean {
    doFirst {
        delete "${buildDir}"
        delete "${projectDir}/node_modules/"
    }
}

task compile(type: NodeTask) {
    dependsOn "npmInstall"
    inputs.dir "$projectDir/src/main/ts"
    inputs.file "$projectDir/webpack.config.js"
    inputs.file "$projectDir/tsconfig.json"
    inputs.file "$projectDir/package.json"
    outputs.dir "$buildDir/dist"
    script = file("node_modules/webpack/bin/webpack.js")
}

task lint(type:NodeTask) {
    dependsOn "compile"
    inputs.dir "$projectDir/src/main/ts"
    //outputs.dir "$buildDir/dist" //this is not correct, lint doesn't have outputs
    script = file("node_modules/tslint/bin/tslint")
    args = ["-p", "$projectDir/src/main/ts", "-c", "$projectDir/../tslint.json", "--fix"]
}

task build {
    dependsOn "lint"
}

task generate(type: NodeTask) {
    dependsOn build
    inputs.dir "$buildDir/dist"
    outputs.dir "$buildDir/generated"
    script = file("$buildDir/dist/bundle.js")
    if (System.env.NODE_DEBUG_OPTION != null) {
        options = System.env.NODE_DEBUG_OPTION.tokenize(' ')
    }
}

